
<!--
```agda
open import Algebra.Bundles using (Group)
open import Level using (0â„“)
module _ {ğ“– : Group 0â„“ 0â„“} where

open Group ğ“– renaming (refl to â‰ˆ-refl; setoid to G)

module _ where
  open import Relation.Binary.Reasoning.Setoid G
```
-->

## Introduction

Commonly, when one starts to learn group theory, they are given the axioms for a group and then shown a very basic proof which very carefully uses these axioms. As an example, one might show that the identity of a group is unique:

```agda
  identityË¡-unique : âˆ€ x y â†’ x âˆ™ y â‰ˆ y â†’ x â‰ˆ Îµ
  identityË¡-unique x y eq = begin
    x              â‰ˆË˜âŸ¨ identityÊ³ x âŸ©
    x âˆ™ Îµ          â‰ˆË˜âŸ¨ âˆ™-congË¡ (inverseÊ³ y) âŸ©
    x âˆ™ (y âˆ™ y â»Â¹) â‰ˆË˜âŸ¨ assoc x y (y â»Â¹) âŸ©
    (x âˆ™ y) âˆ™ y â»Â¹ â‰ˆâŸ¨ âˆ™-congÊ³ eq âŸ©
         y  âˆ™ y â»Â¹ â‰ˆâŸ¨ inverseÊ³ y âŸ©
    Îµ              âˆ
```

This works nicely for some small examples but as proofs get more complicated, a lot of the proof becomes moving brackets around and adding units, which can be considered boilerplate and obscures the interesting parts of the proof.

Instead of tackling these problems in group theory, it is usual to sweep all of these problems under the rug, not write any brackets anywhere, and assume that all the associativity problems could be solved if needed. Luckily for group theorists, it is rarely necessary to worry about these details. Unfortunately if instead we are interested in formalising some group theory, in a proof assistant such as [Agda](https://github.com/agda/agda), then this all becomes very necessary, as the computer does not trust us that all the associativity problems are unnecessary.

In this post, a method in which this "bracketless group theory" can be written in a proof assistant is presented, and some of the difficulties with this are described.

## The Idea

The key concept that will be used is that there are two different types of equality in Type Theory (which is the perspective taken for this post). The first type is propositional equality, which is written as `â‰ˆ` above. `A â‰ˆ B` effectively means that we can prove that `A` and `B` are equal. The second type of equality is definitional equality or judgmental equality. This is stronger than propositional equality and if two objects are definitionally equal it means that they are the same objects (or at least have the same normal form in the type theory).

What we had above was a propositional equality between `(x âˆ™ y) âˆ™ z` and `x âˆ™ (y âˆ™ z)`
```agda
  gassoc : âˆ€ x y z â†’ (x âˆ™ y) âˆ™ z â‰ˆ x âˆ™ (y âˆ™ z)
  gassoc x y z = assoc x y z
```

when what we really want is for this to be a definitional equality, which would allow us to treat these as the same object. We say that we have /strict associativity/ exactly when this equation holds by definition. Luckily there is such a structure that has this property, namely function composition. Suppose we have functions `f g h : X â†’ X`. Then we have
<!--
```agda
module hidden where
  open import Relation.Binary.PropositionalEquality
  infixr 5 _âˆ˜_
  _âˆ˜_ : âˆ€ {X : Set} (f g : X â†’ X) â†’ X â†’ X
  (f âˆ˜ g) x = f (g x)
  infixr 4 _â‰ƒ_
  _â‰ƒ_ : âˆ€ {X Y : Set} (f g : X â†’ Y) â†’ Set
  _â‰ƒ_ {X} f g = (x : X) â†’ f x â‰¡ g x
```
-->
```agda
  sassoc : âˆ€ {X} (f g h : X â†’ X) â†’ (f âˆ˜ g) âˆ˜ h â‰ƒ f âˆ˜ (g âˆ˜ h)
  sassoc f g h = Î» x â†’ refl
```
Therefore we can work with functions without ever having to worry about associativity problems. We can also note that we also eliminate the need for any identity laws with functions as for all sets `X`, we have an identity function `id : X â†’ X` with
```agda
  id : âˆ€ {X : Set} â†’ X â†’ X
  id x = x
```

```agda
  sidentityË¡ : âˆ€ {X} (f : X â†’ X) â†’ id âˆ˜ f â‰ƒ f
  sidentityË¡ f = Î» x â†’ refl
```

```agda
  sidentityÊ³ : âˆ€ {X} (f : X â†’ X) â†’ f âˆ˜ id â‰ƒ f
  sidentityÊ³ f = Î» x â†’ refl
```

We say a structure has /strict units/ or is /strictly unital/ when this property holds.

### Cayley's Theorem
   Cayley's theorem gives us a translation from group operations to function composition. To understand what is says, we first need to introduce the notion of the symmetric group.

   <div class="definition">
   Given a set `X`, the symmetric group on `X`, written `Sym(X)`, is the group whose elements are permutations on `X`, operation is function composition, and inverses and identity are given by inverse and identity permutations.
   </div>

   The important thing of note here is that the group operation in the symmetric group is function composition, just as we wanted. We can now state Cayley's Theorem:

   <div class="theorem">
   Given a group `G`, there is an embedding (an injective homomorphism) from `G` into `Sym(G)`.
   </div>

   <div class="proof">
   Sketch: Define a homomorphism `âŸ¦_âŸ§ : G â†’ Sym(G)` by `âŸ¦ g âŸ§ = Î» x â†’ g âˆ™ x`. It is then routine to show that `âŸ¦_âŸ§` is well defined (`âŸ¦ g âŸ§` is a permutation for each `g`), is a homomorphism, and is injective.
   </div>

   Suppose we want to prove `eâ‚ â‰ˆ eâ‚‚`. The plan is to do the following steps.

   + Embed `eâ‚` and `eâ‚‚` into `Sym(G)` with `âŸ¦_âŸ§`
   + Prove that `âŸ¦ eâ‚ âŸ§` and `âŸ¦ eâ‚‚ âŸ§` are equal as functions, with the benefit that function composition is strictly associative and unital.
   + Use injectivity to get that `eâ‚ â‰ˆ eâ‚‚`

## Implementation
  All the ideas above have been implemented in [https://github.com/alexarice/Groups](https://github.com/alexarice/Groups). The corresponding version of the proof in the introduction is:
<!--
```agda
module _ where
  open import Algebra.Group.Symmetric ğ“– renaming (e to id)
  open import Algebra.Group.Symmetric.Equality ğ“–
  open import Algebra.Group.Reasoning ğ“–
  open import Algebra.Group.Properties SymGroup using (invÊ³; invË¡)
```
-->
```agda
  identity-is-unique-strong : âˆ€ a b â†’ a âˆ™ b â‰ˆ b â†’ a â‰ˆ Îµ
  identity-is-unique-strong a b p = beginâŸ¨ âŸ¦âŸ§ âŸ©
    âŸ¦ a âŸ§                       âˆ˜âŸ¨âŸ©â‰£Ë˜âŸ¨ invÊ³ âŸ¦ b âŸ§ âŸ©
    âŸ¨ âŸ¦ a âŸ§ âˆ˜ âŸ¦ b âŸ§ âŸ©âˆ˜ inv âŸ¦ b âŸ§   â‰£âŸ¨ âŸ¨ âŸ¦âŸ§ âŸ©â¦… p â¦† âŸ©
    âŸ¨ âŸ¦ b âŸ§ âˆ˜ inv âŸ¦ b âŸ§           âŸ©â‰£âŸ¨ invÊ³ âŸ¦ b âŸ§ âŸ©
    id                              âˆ
```

  As a second example, here is the proof of left cancellation:

```agda
  left-cancellation : âˆ€ g h x â†’ x âˆ™ g â‰ˆ x âˆ™ h â†’ g â‰ˆ h
  left-cancellation g h x p = beginâŸ¨ âŸ¦âŸ§ âŸ©
    âŸ¨âŸ©âˆ˜ âŸ¦ g âŸ§ â‰£Ë˜âŸ¨ invË¡ âŸ¦ x âŸ§ âŸ©
    inv âŸ¦ x âŸ§ âˆ˜âŸ¨ âŸ¦ x âŸ§ âˆ˜ âŸ¦ g âŸ§ âŸ©â‰£âŸ¨ âŸ¨ âŸ¦âŸ§ âŸ©â¦… p â¦† âŸ©
    âŸ¨ inv âŸ¦ x âŸ§ âˆ˜ âŸ¦ x âŸ§ âŸ©âˆ˜ âŸ¦ h âŸ§ â‰£âŸ¨ invË¡ âŸ¦ x âŸ§ âŸ©
    âŸ¦ h âŸ§ âˆ
```

  and that the inverse operation is an anti homomorphism on the group operation:

```agda
  inv-of-composite : âˆ€ g h â†’ (g âˆ™ h) â»Â¹ â‰ˆ h â»Â¹ âˆ™ g â»Â¹
  inv-of-composite g h = beginâŸ¨ âŸ¦âŸ§ âŸ©
    inv (âŸ¦ g âŸ§ âˆ˜ âŸ¦ h âŸ§) âˆ˜âŸ¨âŸ©â‰£Ë˜âŸ¨ invÊ³ âŸ¦ g âŸ§ âŸ©
    inv (âŸ¦ g âŸ§ âˆ˜ âŸ¦ h âŸ§) âˆ˜ âŸ¦ g âŸ§ âˆ˜âŸ¨âŸ©âˆ˜ inv âŸ¦ g âŸ§ â‰£Ë˜âŸ¨ invÊ³ âŸ¦ h âŸ§ âŸ©
    âŸ¨ inv (âŸ¦ g âŸ§ âˆ˜ âŸ¦ h âŸ§) âˆ˜ (âŸ¦ g âŸ§ âˆ˜ âŸ¦ h âŸ§) âŸ©âˆ˜ inv âŸ¦ h âŸ§ âˆ˜ inv âŸ¦ g âŸ§
      â‰£âŸ¨ invË¡ (âŸ¦ g âŸ§ âˆ˜ âŸ¦ h âŸ§) âŸ©
    inv âŸ¦ h âŸ§ âˆ˜ inv âŸ¦ g âŸ§ âˆ
```

  The rest of the post will be explaining the code above.

### Elimination of cong
   Perhaps the most obvious change from the original code is the addition of the angle brackets `âŸ¨` and `âŸ©`. These allow us to apply an equality to a part of the expression we are working on. Instead of having to manually use a `cong` function to manipulate the equality (as well as making it unreadable), we instead use the syntax above which allows the appropriate equalities to be automatically put in.

   This is made possible due to the simplicity of the expressions. As each expression is now effectively a finite list of functions, it becomes very easy to split it into a focus, which we manipulate, as well as expressions that occur before and after the focus.

### Strict associativity and unitality
   The above proof shows that the need for associativity operations has been removed. The result of the first equality in the proof is:

```agda-no-compile
âŸ¦ a âŸ§ âˆ˜ (âŸ¦ b âŸ§ âˆ˜ inv âŸ¦ b âŸ§)
```

and yet in the next line we treat it as:

```agda-no-compile
(âŸ¦ a âŸ§ âˆ˜ âŸ¦ b âŸ§) âˆ˜ inv âŸ¦ b âŸ§
```

without telling Agda how to deal with this. This is possible as the terms have the same normal form (this is not actually true, see [Equality between functions](#equality-between-functions)).

We can also see that unitality has been taken care of. In the first line it has been implicitly assumed that an identity can be inserted on the right of the expression.

## Equality between functions
   The choice of equality between functions is crucial to making this syntax work. The complication is that we have said function composition is strictly associative but glossed over that we are working with permutations or invertible functions.

   Above, the `âˆ˜` operator is not just composition of functions, but is composition of invertible functions, which joins the proofs that each component is invertible to get a proof that the whole composite is invertible. It turns out this is /not/ a strictly associative operation. However if we make sure we only compare the function components of the invertible functions, then strict associativity returns.

   This would lead us to the following definition of equality between invertible functions.

<!--
```agda
module hidden2 where
  open import Algebra.Group.Symmetric ğ“– renaming (e to id)
  open import Function.Equality using (_â‡¨_;Î ;_âŸ¶_) renaming (_âˆ˜_ to _*_)
  open import Function.Inverse using (Inverse)
  open import Level
  open import Relation.Binary using (Setoid; _â‡’_)

  open Î 
  open Inverse

  funcSetoid : Setoid _ _
  funcSetoid = G â‡¨ G
  open module F = Setoid funcSetoid using () renaming (_â‰ˆ_ to _â‰ƒ_)
```
-->

```
  _âˆ¼_ : Sym â†’ Sym â†’ Set
  f âˆ¼ g = to f â‰ƒ to g
```

Where Sym is the type of permutations on the group and `â‰ƒ` is pointwise equality of functions.

 This has the advantage that it does not remember any of the invertibility data and so we can have all the nice features mentioned above. However, it turns out that this forgets too much invertibility data, in that if the implementation is made with this equality, then Agda will complain everywhere about implicit arguments that it cannot resolve. The next thing we might try is to remember the invertibility data but do not check whether it is equal. This leads to the following definition.

```agda
  record SymEq (f g : Sym) : Set where
    field
      eq : to f â‰ƒ to g

  _â‰£_ : Sym â†’ Sym â†’ Set
  _â‰£_ = SymEq
```

This ends up being a nice form of equality which we use to define the symmetric group. Unfortunately, it runs into problems with invertibility data not lining up if we try to use it in the syntax above. It turns out we need another type of equality, where we only remember the invertibility data for the second argument. This clears all the implicit argument problems without introducing any associativity problems.

```
  record PartSymEq (f : G âŸ¶ G) (g : Sym) : Set where
    field
      peq : f â‰ƒ to g

  _â‰£'_ : Sym â†’ Sym â†’ Set
  f â‰£' g = PartSymEq (to f) g
```

### Dealing with homomorphisms
   So far we have not covered the last step of the proof strategy, which was using injectivity transform function equality to group element equality. This is a very simple procedure, however it does not usually do what we want. Consider the proof that identities are unique. Injectivity gives us something of the form:

```agda-no-compile
  âŸ¦ a âŸ§ â‰£ âŸ¦ Îµ âŸ§ â†’ a â‰ˆ Îµ
```

but what we actually wanted was:

```agda-no-compile
  âŸ¦ a âŸ§ â‰£ id â†’ a â‰ˆ Îµ
```

We also have the same problem going the opposite direction. In the proof we have an argument of the form:

```agda-no-compile
  a âˆ™ b â‰ˆ b
```

If were naively transport this using the inclusion we would get:

```agda-no-compile
  âŸ¦ a âˆ™ b âŸ§ â‰£ âŸ¦ b âŸ§
```

when what we really wanted was:

```agda-no-compile
  âŸ¦ a âŸ§ âˆ˜ âŸ¦ b âŸ§ â‰£ âŸ¦ b âŸ§
```

The proofs that these imply each other are not too difficult but applying them to each case is tedious and makes the proofs more convoluted which defeats the original purpose of this construction. Therefore two reflection helpers have been made to automate this process. The first is `âŸ¨_âŸ©â¦…_â¦†`, which solves the second problem. This takes a homomorphism and an equality and applies as many homomorphism rules as possible to the equality. It does this by inspecting the terms of the left and right hand side using reflection and using this to work out which rules to apply.

<!--
```agda
module _ where
  open import Algebra.Group.Symmetric ğ“– renaming (e to id)
  open import Algebra.Group.Symmetric.Equality ğ“–
  open import Algebra.Group.Reasoning ğ“–
```
-->

```agda
  test : âˆ€ a b â†’ a â‰ˆ b â†’ âŸ¦ a âŸ§ â‰£ âŸ¦ b âŸ§
  test a b p = âŸ¨ âŸ¦âŸ§ âŸ©â¦… p â¦†

  test2 : âˆ€ a b â†’ a âˆ™ b â‰ˆ b â†’ âŸ¦ a âŸ§ âˆ˜ âŸ¦ b âŸ§ â‰£ âŸ¦ b âŸ§
  test2 a b p = âŸ¨ âŸ¦âŸ§ âŸ©â¦… p â¦†

  test3 : âˆ€ x y z
        â†’ x âˆ™ (y â»Â¹ âˆ™ Îµ) â‰ˆ z âˆ™ z
        â†’ âŸ¦ x âŸ§ âˆ˜ (inv âŸ¦ y âŸ§ âˆ˜ id) â‰£ âŸ¦ z âŸ§ âˆ˜ âŸ¦ z âŸ§
  test3 x y z p = âŸ¨ âŸ¦âŸ§ âŸ©â¦… p â¦†
```

The second reflection helper is wrapped in the `beginâŸ¨_âŸ©_` function. This takes care of the first problem we had, again by using reflection to find the term needed and applying the appropriate rules to get the given proof in the form needed.

## Limitations
There are currently a few limitations with this system:
- The syntax is not ideal. One problem is that it currently seems necessary to pass the homomorphism into the reflection helper each time it is used though this is probably fixable. Something which is less fixable is the angle bracket syntax for dealing with congs being "stuck to" the equality symbol. This is caused by Agda syntax requiring that you alternate holes and non holes, where we would like to be able to put two non holes next to each other.
- The reflection helpers are very slow. In the current version of Agda (2.6.1), compiling these files is very slow which makes them painful to work with.
- Agda is not great at telling you what should go in a hole with this syntax. Where the reflection helpers are used, it will tell you it wants something of a type labelled by some mysterious number. Even when this isn't the case, the use of records for the permutations sometimes causes a mess when one tries to solve a hole.

## Thanks
Thanks goes to MartÃ­n EscardÃ³, who suggested the possibility of using Cayley's Theorem and convinced me to write this post. I would also like to thank all members of the Birmingham theory group who I have discussed this work with.
